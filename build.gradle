import org.jetbrains.changelog.Changelog

plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "0.8.4"
    id 'org.jetbrains.changelog' version '2.+'
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url "https://api.modrinth.com/maven/" }
    maven { url 'https://maven.nucleoid.xyz' }
    maven {
        name 'Xander Maven'
        url 'https://maven.isxander.dev/releases'
    }
    maven { url "https://maven.resourcefulbees.com/repository/maven-public/" }
}


dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation("maven.modrinth:friends-and-foes:${project.friends_and_foes_version}") {
        exclude(group: "net.fabricmc")
    }

    modImplementation("dev.isxander:yet-another-config-lib:${project.yacl_version}")
    modImplementation("com.teamresourceful.resourcefullib:${project.resourcefullib_version}")

    modImplementation include("eu.pb4:factorytools:${project.factorytools_version}")
    modImplementation "eu.pb4:polymer-core:${project.polymer_version}"
    modImplementation("eu.pb4:polymer-blocks:${project.polymer_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "eu.pb4:polymer-resource-pack:${project.polymer_version}"
    modImplementation("eu.pb4:polymer-sound-patcher:${project.polymer_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "eu.pb4:polymer-resource-pack-extras:${project.polymer_version}"
    modImplementation("eu.pb4:polymer-autohost:${project.polymer_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "eu.pb4:polymer-virtual-entity:${project.polymer_version}"
}

publishMods {
    file = remapJar.archiveFile
    type = STABLE
    changelog = fetchChangelog()

    displayName = "Polymer Patch for Friends&Foes ${version.get()}"
    modLoaders.add("fabric")
    modLoaders.add("quilt")

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "67luhYti"
        minecraftVersions.add(project.minecraft_version)
        requires("friends-and-foes", "polymer")
    }
    github {
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = providers.environmentVariable("GITHUB_REPOSITORY").getOrElse("DrexHD/dryrun")
        commitish = providers.environmentVariable("GITHUB_REF_NAME").getOrElse("dryrun")
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

private String fetchChangelog() {
    def log = getChangelog.changelog.get()
    if (log.has(project.mod_version)) {
        return log.renderItem(
                log.get(project.mod_version).withHeader(false),
                Changelog.OutputType.MARKDOWN
        )
    } else {
        return ""
    }
}